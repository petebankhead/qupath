plugins {
    id 'qupath.java-conventions'
    id 'org.bytedeco.gradle-javacpp-platform'
    id 'jacoco'
}

repositories {
    mavenLocal()
    mavenCentral()

    // May be required for snapshot ImageJ2 jars
    maven { url 'https://maven.imagej.net/content/groups/public' }

    // Required for Bio-Formats
    maven {
        name 'Unidata'
        url 'https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases'
    }
    maven { url 'https://artifacts.openmicroscopy.org/artifactory/maven/' }

    // May be required for snapshot JavaCPP jars
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

    // Currently required for OpenSlide
    maven { url "../maven/repo" }
}


/*
 * Some metadata for the manifest
 */
project.version = project.file('../VERSION').text.strip()


/*
 * Placeholder to use OpenCV binaries with GPU support... if/when this is supported (warning: they are quite big)
 */
def useGPU = false
// TODO separate out TensorFlow extension
def includeTensorFlowGPU = (findProperty('tensorflow-gpu') ?: "false") != "false"

/*
 * Handle OS-specific decisions
 */

import org.apache.tools.ant.taskdefs.condition.Os

def platform
def nativesClassifier
String iconName
if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    nativesClassifier = 'natives-windows'
    if (System.properties['sun.arch.data.model'] == '32') {
        logger.warn("You appear to be using a 32-bit JDK - the build will probably fail! At the very least, some things won't work.")
    }
    platform = 'win'
    iconName = 'windows/QuPath.ico'
} else if (Os.isFamily(Os.FAMILY_MAC)) {
    nativesClassifier = 'natives-osx'
    platform = 'mac'
    iconName = 'macosx/qupath.icns'
} else if (Os.isFamily(Os.FAMILY_UNIX)) {
    nativesClassifier = 'natives-linux'
    platform = 'linux'
    iconName = 'linux/QuPath.png'
} else {
    logger.warn('Unknown operating system!')
}


/*
 * JavaFX version
 */
def jfxVersion = '16'
def jfxModules = ['javafx.base', 'javafx.controls', 'javafx.graphics', 'javafx.media', 'javafx.web', 'javafx.swing']


ext {
    // Dependency versions
    bioformatsVersion  = '6.6.1'
    commonsMathVersion = '3.6.1'
    commonsTextVersion = '1.9'
    controlsfxVersion  = '11.1.0'
    groovyVersion      = '3.0.8'
    gsonVersion        = '2.8.6'
    guavaVersion       = '30.1.1-jre'
    imagejVersion      = '1.53j'
    jfxtrasVersion     = '11-r2'
    jpenVersion        = '2-150301'
    jtsVersion         = '1.18.1'
    openslideVersion   = '3.4.1_2'
    richtextVersion    = '0.10.6'
    picocliVersion     = '4.6.1'
    jfreesvgVersion    = '4.2'
    // Note, if OpenCV is a SNAPSHOT version then it must already be installed locally (with Maven)
    javacppVersion     = '1.5.5'
    opencvVersion      = "4.5.1-${javacppVersion}"

    // Additional versions
    logbackVersion     = '1.2.3'
    slf4jVersion       = '1.7.30'
    junitVersion       = '5.7.1'

    // Optional versions
    tensorflowVersion  = "0.3.1" // Really 2.4.1
}

configurations {
    jts
    groovy
    richtextfx
    commonsmath
    commonstext
    gson
    controlsfx
    jfxtras
    opencv
    jpen
    imagej
    bioformats
    openslide
    javafx
    nativeloader
    guava
    picocli
    jfreesvg
    logback

    // Testing
    junit

    // Optional
    tensorflow
}

dependencies {

    jts "org.locationtech.jts:jts-core:${jtsVersion}"
    // Optionally add GeoJSON support (brings in json-simple as sub-dependency)
    // However, the use of simple-json is troublesome since it brings in an old version of junit
//   jts "org.locationtech.jts.io:jts-io-common:${jtsVersion}"

    groovy "org.codehaus.groovy:groovy:${groovyVersion}"
    groovy "org.codehaus.groovy:groovy-jsr223:${groovyVersion}"
    groovy "org.codehaus.groovy:groovy-xml:${groovyVersion}"

    for (fx in jfxModules.collect{it.replace('.', '-')}) {
        javafx "org.openjfx:${fx}:${jfxVersion}"
        javafx "org.openjfx:${fx}:${jfxVersion}:${platform}"
    }

    richtextfx "org.fxmisc.richtext:richtextfx:${richtextVersion}"

    commonsmath "org.apache.commons:commons-math3:${commonsMathVersion}"

    commonstext "org.apache.commons:commons-text:${commonsTextVersion}"

    gson "com.google.code.gson:gson:${gsonVersion}"

    controlsfx "org.controlsfx:controlsfx:${controlsfxVersion}",  {
        // ControlsFX 11.0.1 uses a linux classifier to bring in more JavaFX than it may need
        exclude group: 'org.openjfx'
    }

    jfxtras "org.jfxtras:jfxtras-menu:${jfxtrasVersion}", {
        exclude group: 'org.jfxtras', module: 'jfxtras-test-support'
    }

    if (useGPU) {
        opencv "org.bytedeco:opencv-platform-gpu:${opencvVersion}"
    } else
        opencv "org.bytedeco:opencv-platform:${opencvVersion}"

    jpen "net.sourceforge.jpen:jpen:${jpenVersion}"
    if (nativesClassifier != null)
        jpen "net.sourceforge.jpen:jpen:${jpenVersion}:${nativesClassifier}"

    imagej "net.imagej:ij:${imagejVersion}"

    bioformats "ome:formats-gpl:${bioformatsVersion}", {
        exclude group: 'xalan', module: 'serializer'
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'io.minio', module: 'minio'
        exclude group: 'commons-codec', module: 'commons-codec'
        exclude group: 'commons-logging', module: 'commons-logging'
//        exclude group: 'edu.ucar', module: 'cdm'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.code.findbugs', module: 'annotations'
    }

    openslide "org.openslide:openslide:${openslideVersion}"
    if (nativesClassifier != null)
        openslide "org.openslide:openslide:${openslideVersion}:${nativesClassifier}"

    guava "com.google.guava:guava:${guavaVersion}", {
        exclude group: 'com.google.code.findbugs'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.checkerframework', module: 'checker-qual'
    }

    picocli "info.picocli:picocli:${picocliVersion}"

    jfreesvg "org.jfree:org.jfree.svg:${jfreesvgVersion}"

    junit "org.junit.jupiter:junit-jupiter:${junitVersion}"
//    junit "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    logback "ch.qos.logback:logback-classic:${logbackVersion}"
    logback "org.slf4j:slf4j-api:${slf4jVersion}"

    if (includeTensorFlowGPU) {
        tensorflow "org.tensorflow:tensorflow-core-platform-gpu:${tensorflowVersion}"
    } else {
        tensorflow "org.tensorflow:tensorflow-core-platform:${tensorflowVersion}"
    }

}


configurations {
    implementation.extendsFrom logback
    testImplementation.extendsFrom junit
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.named('test') {
    useJUnitPlatform()
}
